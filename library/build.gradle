import java.nio.file.Paths

plugins {
  id "com.android.library" version "$_gradlePluginVersion"
}

final def useGit = System.env.CI != null && "which git".execute().waitFor() == 0
android {
  buildToolsVersion _buildToolsVersion
  compileSdkVersion _compileSdkVersion.toInteger()
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  defaultConfig {
    minSdkVersion 1
    targetSdkVersion 30
    versionCode useGit ? "git rev-list --count HEAD".execute().text.toInteger() : Integer.MAX_VALUE
    versionName useGit ? "git describe --abbrev=0".execute().text.trim() : String.valueOf(versionCode)
    resConfigs "en"
  }
  variantFilter {
    if (it.buildType.name == "debug") {
      it.ignore = true
    }
  }
}

repositories {
  mavenCentral()
  google()
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
  }
}

if (System.env.CI != null && "which git".execute().waitFor() == 0) {
  apply from: Paths.get(projectDir.absolutePath, 'publish.gradle')
}
